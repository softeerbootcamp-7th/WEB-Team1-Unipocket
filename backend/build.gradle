plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.diffplug.spotless' version '8.1.0'
}

group = 'com.genesis'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Plugins
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Redis Driver
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// lombok & MapStruct
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'org.mapstruct:mapstruct:1.6.3'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

	// MySQL Driver
	runtimeOnly 'com.mysql:mysql-connector-j'
	// H2 Database
	developmentOnly 'com.h2database:h2'

	// test libraries & containers
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:mysql'
	testImplementation 'com.redis:testcontainers-redis:2.2.2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// JWT Related Dependencies
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

	// H2 Database (for testing)
	testRuntimeOnly 'com.h2database:h2'

	// AWS S3
	implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:3.1.0")
	implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3'
}

tasks.register("unitTest", Test) {
	group = "verification"
	description = "Runs unit tests (excluding integration tests)."

	useJUnitPlatform {
		excludeTags "integration"
	}

	// fork PR에서도 항상 돌아야 하므로 test 프로파일 고정(더미 설정/인메모리 DB 등)
	systemProperty "spring.profiles.active", "test-unit"
}

tasks.register("integrationTest", Test) {
	group = "verification"
	description = "Runs integration tests (tagged with 'integration')."

	useJUnitPlatform {
		includeTags "integration"
	}

	systemProperty "spring.profiles.active", "test-it"
	shouldRunAfter tasks.named("unitTest")
}

spotless {

	format 'misc', {
		target '*.gradle', '.gitattributes', '.gitignore'

		trimTrailingWhitespace()
		leadingSpacesToTabs()
		endWithNewline()
	}

	format 'miscYAML', {
		target '**/*.yaml', '**/*.yml'

		prettier()
		trimTrailingWhitespace()
		endWithNewline()
	}

	java {
		googleJavaFormat('1.19.1').aosp().reflowLongStrings().skipJavadocFormatting()

		formatAnnotations()
		removeUnusedImports()
		leadingSpacesToTabs()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

bootJar {
	archiveFileName = 'app.jar'
}
